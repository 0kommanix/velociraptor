buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4'
        //noinspection GradleDependency
        classpath 'com.squareup.sqldelight:gradle-plugin:0.6.1'
        classpath 'com.google.gms:oss-licenses:0.9.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.0"

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_VELOCIRAPTOR
                keyPassword RELEASE_KEY_PASSWORD_VELOCIRAPTOR
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.pluscubed.velociraptor"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 43
        versionName "1.6.6"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [appName: "@string/app_name"]
        multiDexEnabled true

        lintOptions {
            disable 'MissingTranslation'
            disable 'ExtraTranslation'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debugIap {
            debuggable true
            minifyEnabled false
            shrinkResources false

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debug {
            minifyEnabled false
            useProguard false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled false
            manifestPlaceholders = [appName: "Velociraptor DEBUG"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.robolectric:robolectric:3.7.1'

    debugImplementation 'im.dino:dbinspector:3.4.1@aar'
    debugIapImplementation 'im.dino:dbinspector:3.4.1@aar'

    implementation 'com.google.auto.value:auto-value:1.4'
    annotationProcessor "com.google.auto.value:auto-value:1.4"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    implementation 'com.squareup.sqlbrite:sqlbrite:1.1.2'

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:support-v13:$support_version"
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation "com.google.firebase:firebase-core:16.0.1"
    implementation "com.google.firebase:firebase-messaging:17.0.0"
    implementation "com.google.firebase:firebase-config:16.0.0"

    implementation "com.android.billingclient:billing:1.1"

    implementation "com.google.android.gms:play-services-location:$play_version"
    implementation "com.google.android.gms:play-services-oss-licenses:$play_version"
    implementation "com.google.android.gms:play-services-maps:$play_version"
    implementation "com.google.maps.android:android-maps-utils:0.5"

    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.6'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation 'com.squareup.okio:okio:1.14.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'

    implementation 'com.github.gigamole.arcprogressstackview:library:1.0.3'

    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.2'

    implementation 'com.github.bumptech.glide:glide:3.7.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.jakewharton.timber:timber:4.7.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    debugIapImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


apply plugin: 'com.google.gms.google-services'